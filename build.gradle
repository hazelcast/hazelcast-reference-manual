buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.7'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
        classpath 'org.asciidoctor:asciidoctorj-epub3:1.5.0-alpha.8.1'
        classpath 'org.asciidoctor:asciidoctorj:1.6.0'
    }
}

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'idea'
apply plugin: 'java'

ext {
    imdgVersion = '4.1.2-SNAPSHOT'
}

repositories {
    mavenCentral()
    maven {
        url "https://repository.hazelcast.com/release"
    }
    maven {
        url "https://repository.hazelcast.com/snapshot"
    }
    mavenLocal()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url  "https://dl.bintray.com/bmuschko/maven" 
    }

}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
     compile "com.hazelcast:hazelcast-enterprise:$imdgVersion"
     compile "com.atomikos:transactions-jta:4.0.6"
     compile "javax.transaction:javax.transaction-api:1.3"
     compile "javax.cache:cache-api:1.1.1"
     compile "org.mongodb:mongo-java-driver:3.0.4"
     compile "junit:junit:4.12"
     asciidoctor 'com.bmuschko:asciidoctorj-tabbed-code-extension:0.2'
}

asciidoctor {
    dependsOn compileJava
    inputs.files compileJava.outputs

    sources {
      include 'index.adoc'
    }

    backends = [ 'html5' , 'pdf' /*, 'multipage_html5', 'epub3'*/ ]

    if (project.hasProperty('skipPdf')) {
	println "Skipping generation of PDF due to -PskipPdf flag set"
        backends.remove('pdf')
    }
	
    attributes \
        'imdg-version': project.imdgVersion,
        'docBaseUrl': 'https://docs.hazelcast.org/docs/' + project.imdgVersion,
        'javasource' : project.sourceSets.main.java.srcDirs[0].absolutePath,
        'javatest' : project.sourceSets.test.java.srcDirs[0].absolutePath,
        'javatestresource' : project.sourceSets.test.resources.srcDirs[0].absolutePath

    requires project.file('enable-verbose-mode.rb')

}

build {
   dependsOn asciidoctor
}
